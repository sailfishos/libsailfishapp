#
# sailfishapp_i18n.prf: Qt Feature file for Translated Sailfish apps
# Usage: CONFIG += sailfishapp_i18n
#
# Copyright (c) 2014 Jolla Ltd.
# Contact: Thomas Perl <thomas.perl@jolla.com>
# All rights reserved.
#
# This file is part of libsailfishapp
#
# You may use this file under the terms of BSD license as follows:
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of the Jolla Ltd nor the
#       names of its contributors may be used to endorse or promote products
#       derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDERS OR CONTRIBUTORS BE LIABLE FOR
# ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#

TS_FILE = \"$${_PRO_FILE_PWD_}/translations/$${TARGET}.ts\"

# Translation source directories
TRANSLATION_SOURCE_CANDIDATES = $${_PRO_FILE_PWD_}/src $${_PRO_FILE_PWD_}/qml
for(dir, TRANSLATION_SOURCE_CANDIDATES) {
    exists($$dir) {
        TRANSLATION_SOURCES += \"$$dir\"
    }
}

# prefix all TRANSLATIIONS with the src dir
for(t, TRANSLATIONS) {
    XLATION_IN += \"$${_PRO_FILE_PWD_}/$$t\"
}
# The target would really be $$TS_FILE, but we use a non-file target to emulate .PHONY
update_translations.target = update_translations
# update the ts files in the src dir and then copy them to the out dir
update_translations.commands += lupdate $${TRANSLATION_SOURCES} -ts $${TS_FILE} $$XLATION_IN && \
    mkdir -p translations && \
    [ \"$${OUT_PWD}\" != \"$${_PRO_FILE_PWD_}\" ] && \
    cp -af \"$${_PRO_FILE_PWD_}/translations/\"*.ts \"$${OUT_PWD}/translations\" || :
QMAKE_EXTRA_TARGETS += update_translations
PRE_TARGETDEPS += update_translations

# the qm files are generated from the ts files copied to out dir
for (t, TRANSLATIONS) {
    XLATION_OUT += \"$${OUT_PWD}/$$t\"
}
build_translations.target = build_translations
sailfishapp_i18n_idbased {
    build_translations.commands += lrelease -idbased $${XLATION_OUT}
} else {
    build_translations.commands += lrelease $${XLATION_OUT}
}

QMAKE_EXTRA_TARGETS += build_translations
POST_TARGETDEPS += build_translations

qm.files = $$replace(XLATION_OUT, \.ts, .qm)
qm.path = /usr/share/$${TARGET}/translations
qm.CONFIG += no_check_exist

INSTALLS += qm
